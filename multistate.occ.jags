
    model  { 
    
    # Priors
    pNight ~ dunif(0, 1)  
    pDay ~ dunif(0, 1)

    #We need to force our psi parameters and our detection probabilities
    #for state 4 to sum to one. This is how do that using priors.

    for (i in 1:4) {
      beta[i] ~ dgamma(1, 1)   # Induce Dirichlet prior
      pND[i] <- beta[i]/sum(beta[])

      alpha[i] ~ dgamma(1, 1)   # Induce Dirichlet prior
      psi[i] <- alpha[i]/sum(alpha[])
    }

    
    
    # Define state vector
    for (s in 1:R){
      phi[s,1] <- psi[1]
      phi[s,2] <- psi[2]
      phi[s,3] <- psi[3]
      phi[s,4] <- psi[4]
    }
    
    # Define observation matrix
    # Order of indices: true state, time, observed state
    for (t in 1:T){
    p[1,t,1] <- 1
    p[1,t,2] <- 0
    p[1,t,3] <- 0
    p[1,t,4] <- 0
    p[2,t,1] <- 1-pNight
    p[2,t,2] <- pNight
    p[2,t,3] <- 0
    p[2,t,4] <- 0
    p[3,t,1] <- 1-pDay
    p[3,t,2] <- 0
    p[3,t,3] <- pDay
    p[3,t,4] <- 0
    p[4,t,1] <- pND[1]
    p[4,t,2] <- pND[2]
    p[4,t,3] <- pND[3]
    p[4,t,4] <- pND[4]
    }
    
    # State-space likelihood
    # State equation: model of true states (z)
    for (s in 1:R){
     z[s] ~ dcat(phi[s,])
    }
    
    # Observation equation
    for (s in 1:R){
       for (t in 1:T){ 
        y[s,t] ~ dcat(p[z[s],t,])
       } #t
    } #s
    
    # Derived quantities
    for (s in 1:R){
      occ1[s] <- equals(z[s], 1)
      occ2[s] <- equals(z[s], 2)
      occ3[s] <- equals(z[s], 3)
      occ4[s] <- equals(z[s], 4)
    }

    n.occ[1] <- sum(occ1[]) #Number of Sites in state 1
    n.occ[2] <- sum(occ2[]) #Number of Sites in state 2
    n.occ[3] <- sum(occ3[]) #Number of Sites in state 3
    n.occ[4] <- sum(occ4[]) #Number of Sites in state 4
    }
    
